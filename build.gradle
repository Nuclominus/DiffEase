// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        libVersion = "1.0.3"
        sampleVersion = "1.0.0"

        minSdkVersion = 21
        maxSdkVersion = 32
        buildTools = "30.0.3"

        hilt_version = "2.40.1"
        libVersions = [
                appCompat            : "1.4.2",
                constraintlayout     : "2.1.4",
                core                 : "1.8.0",
                material             : "1.6.1",
                navigationFragment   : "2.5.0",
                navigationUI         : "2.5.0",
                recyclerviewSelection: "1.1.0",
                recyclerview         : "1.2.1",
                hilt                 : hilt_version,
                hiltCompiler         : hilt_version,
                viewbindingDelegate  : "1.5.3",
                junit                : "1.1.3",
                espressoCore         : "3.4.0"
        ]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.2.1"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }

}

plugins {
    id "org.jetbrains.kotlin.android" version "1.6.20" apply false
    id "maven-publish"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

/**
 * Return the module dependency for the given compatibility library name.
 */
def compatibility(name) {
    switch (name) {
        case "core":
            return "androidx.core:core:${libVersions.core}"
        case "appcompat":
            return "androidx.appcompat:appcompat:${libVersions.appCompat}"
        case "constraintlayout":
            return "androidx.constraintlayout:constraintlayout:${libVersions.constraintlayout}"
        case "material":
            return "com.google.android.material:material:${libVersions.material}"
        case "navigationFragment":
            return "androidx.navigation:navigation-fragment-ktx:${libVersions.navigationFragment}"
        case "navigationUI":
            return "androidx.navigation:navigation-ui-ktx:${libVersions.navigationUI}"
        case "recyclerview":
            return "androidx.recyclerview:recyclerview:${libVersions.recyclerview}"
        case "recyclerviewSelection":
            return "androidx.recyclerview:recyclerview-selection:${libVersions.recyclerviewSelection}"
        case "hilt":
            return "com.google.dagger:hilt-android:${libVersions.hilt}"
        case "hiltCompiler":
            return "com.google.dagger:hilt-compiler:${libVersions.hiltCompiler}"
        case "viewbindingDelegate":
            return "com.github.kirich1409:viewbindingpropertydelegate-noreflection:${libVersions.viewbindingDelegate}"
        case "recyclerdiffadapter":
            return "io.github.nuclominus:recyclerdiffadapter:$libVersion"
        default:
            throw new IllegalArgumentException("No mapping exists for name: $name.")
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}