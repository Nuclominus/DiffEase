plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

ext {
    groupId = 'io.github.nuclominus'
    artifactId = "recyclerdiffadapter"
}

apply from: rootProject.file('publishing.gradle')

android {
    compileSdk rootProject.ext.maxSdkVersion
    namespace 'com.nuclominus.diffadapter'

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.maxSdkVersion

        consumerProguardFiles 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

}

dependencies {

    // Fix knowing issue with new version of androidx.core
    // https://youtrack.jetbrains.com/issue/KT-54136/Duplicated-classes-cause-build-failure-if-a-dependency-to-kotlin-stdlib-specified-in-an-android-project#focus=Comments-27-6550645.0-0
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
    api (libs.androidx.core)
    api (libs.androidx.appcompat)
    api (libs.material)
    api (libs.recyclerviewSelection)
    api (libs.recyclerview)
}